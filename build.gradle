/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2020-2023 The RIOT authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
config {
    info {
        description = 'RIOT'
        inceptionYear = '2020'
        vendor = 'Redis'
        tags = ['redis', 'tool', 'import', 'export', 'replication']

        links {
            website      = "https://github.com/redis-developer/${project.rootProject.name}"
            issueTracker = "https://github.com/redis-developer/${project.rootProject.name}/issues"
            scm          = "https://github.com/redis-developer/${project.rootProject.name}.git"
        }

        scm {
            url                 = "https://github.com/redis-developer/${project.rootProject.name}"
            connection          = "scm:git:https://github.com/redis-developer/${project.rootProject.name}.git"
            developerConnection = "scm:git:git@github.com:redis-developer/${project.rootProject.name}.git"
        }

        specification {
            enabled = true
        }

        implementation {
            enabled = true
        }
        
        people {
            person {
                id    = 'jruaux'
                name  = 'Julien Ruaux'
                roles = ['developer', 'author']
            }
        }
    }

    docs {
        javadoc {
            enabled = false
        }
    }

    coverage {
        jacoco {
            enabled = true
            toolVersion = jacocoVersion
        }
    }
}

allprojects {
    repositories {
        mavenLocal()
        gradlePluginPortal()
    }

    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
}

subprojects { subproj ->
    if (!subproj.name.contains('guide')) {
    
        config {
	        info {
	            description = project.project_description
	        }
	    }
	
	    license {
	        exclude('build/**')
	    }

	    dependencies {
	        compileOnly group: 'com.google.code.findbugs', name: 'jsr305', version: jsr305Version
            testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitVersion
            testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: junitVersion
            testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitVersion
            testImplementation group: 'org.junit.platform', name: 'junit-platform-launcher', version: junitPlatformVersion
            testImplementation group: 'org.testcontainers', name: 'junit-jupiter', version: testcontainersVersion
	        testImplementation group: 'com.redis', name: 'testcontainers-redis', version: testcontainersRedisVersion
	    }
	
	    test {
	        useJUnitPlatform()
	    }

	    configurations {
	        all*.exclude module: 'spring-boot-starter-logging'
	    }
	    
	    configurations.all {
			resolutionStrategy.eachDependency { DependencyResolveDetails details ->
				if (details.requested.name == 'lettuce-core' ) {
					details.useVersion lettuceVersion
				}
			}
		}
	}
}

subprojects { p ->
    config {
        coverage {
            jacoco {
                enabled = hasTestsAt(p.file('src/test'))
		        toolVersion = jacocoVersion
            }
        }
    }
}

boolean hasTestsAt(File testDir) {
    testDir.exists() && testDir.listFiles()?.size()
}